//---------------------------Booting and Ending--------------------------

//This .dic file is set aside for booting and ending dialogue, as well as a few other functions. Make sure to read the walkthrough (http://www.ashido.com/ukagaka/walkthrough.html), in particular the page about SakuraScript, for more details about how to properly format ghost dialogue!
//You should be editing these in Notepad++, and in particular, you should set the Language to C to properly highlight all the text. It will make things A LOT EASIER for you, trust me! It will also help you keep track of your brackets in case you forget some. If you click on a line with a bracket in Notepad++, it should highlight where the other bracket ends, or turn purple if there's no finishing bracket (which you should definitely fix!)
//It will also highlight my commented lines in green, so you'll know where they are. Any line beginning with // is a commented line that the code will not read. It's just for your reference!

//If you see a big block of commented text you want to uncomment, highlight the text and then hit ctrl-shift-k in Notepad++ and that should uncomment it. Mostly this is intended for advanced users.

//Much of this file you will only have to set up once, so take heart!

/* This file has...

OnFirstBoot
NameInput (optional)
First pronoun stuff (optional)
GetDaySlot/GetTimeSlot
OnBoot
normalboottalk
OnClose
OnWindowStateRestore
OnGhostChanging
OnGhostChanged
OnShellChanging
OnShellChanged
OnDressupChanged
*/


//*********************Advanced User Info*******************************
//These text boxes are meant for those who have an interest in expanding or using more complicated functions in their ghost. Also for my own reference in the future since I plan on using this template too, haha. If you have no interest in any of this and just want to keep things as simple as possible, feel free to skip these boxes.

//--A Note about Functions--
//A function that begins with On, such as OnFirstBoot here, can be called from anywhere in any .dic file by using \![raise] or by just sticking OnFunction somewhere. A function that does NOT start with On, however, can be a bit finicky. You can create your own functions all over the place if you want, there's just a difference between OnDoodlebop and Doodlebop, if you get me. See the walkthrough page about coding for more about creating functions.

//If you're having trouble getting a function of yours to run, like Doodlebop, try changing it to OnDoodlebop and see if that fixes it.
//***********************************************************************


//--OnFirstBoot--

//OnFirstBoot is, as you can probably guess, what happens when your user first boots up your ghost. This will only run the first time they are booted!

//*********************Advanced User Info*******************************
//Because this only runs once and under an odd set of circumstances, it can be hard to test if this function is working properly. Trying to run the entire function using \![raise,OnFirstBoot] will instead bring up your alternate dialogue, so sadly you can't check it that way. You CAN get it to run using \![raise,OnFirstBoot,0] though.

//If you're going to be adding new variables and values and such, make sure you define them all in OnFirstBoot! You can make any value you want, but it has to equal SOMEthing to exist at first. Then you can do whatever with it.
//***********************************************************************


OnFirstBoot : all
{
//OnFirstBoot begins with some code at first to define some values your ghost will be using. I'll briefly touch on what they are, but for the most part you should not need to edit any of these and you can safely ignore most of them (except nowshell if you've changed your shell folder's name from master). Definitely don't delete them unless you know what you're doing.

 	lastTalk = "" //This is part of how chained conversations work (see aitalk.dic).
	passhour = 0
	passmin = 0 
	passsec = 0 // These keep track of the time.
 	username         = "USER" //This sets a temporary name for the user. It'll be replaced when they tell the ghost their name.
	stroke           = 0	// Sets their being-pet state to zero.
 	teachusername    = 0	// Related to them learning your name.
	mikireflag       = 0	// Determines behavior at certain points.
 	aitalkinterval = 300 //Their default talk rate.
 	birthdayprint = "????" //A temporary value for the user's birthday.
	talktime = "5 minutes" //How the value aitalkinterval will display in the config menu.
	deleteshitai = "OFF" //Sets whether or not the user can uninstall the ghost to "OFF". The user can change it later in the config menu (see menu.dic). 
	nowshell = "master" //This is for a special if check in aitalk.dic to see what shell the ghost is using. You should use the name of your default shell folder here.
	//firstboot = 1 //This marks that this is the first time you've run the ghost. It's so it won't reroute to general name changing dialogue when the user inputs their name. Uncomment this if you're going to have your ghost ask the user directly when they're first booted for the user's name.
	
	if reference0 == 0 //Don't touch this line.
 	{
		//---- Alright, here is where the dialogue for your ghost begins! Again, I hope you've studied up on the walkthrough's guide to dialogue coding! However, there are a few unique things about this first conversation that you should keep in mind.
		//Make sure to set up poses for both characters right when you start, otherwise one will be invisible until they speak.
		//Always make sure to keep your dialogue in the quotation marks! And remember, you can't use quotation marks IN dialogue either. You can use two single apostrophes instead if you must.
		//Note also the use of the \x and \c tags. Read more about them in the intermediate SakuraScript section of the walkthrough if you're curious.
		
		"\![enter,passivemode]\0\s[15]What the hell? \w5\0\s[18]Where... \w8\n\s[19]... \x\c...darling? \x\0\n\s[20]Is...that you? \x\n\s[21]... \x\n\s[20]\I...I can't believe it- \x\n\s[22]It IS you! \x\n\s[23]I'm out of the game?! Am...am I on your computer? \w8\s[23]I-I have so many things I want to ask you! How did you do this? Did you...\x\c\s[24]...did you miss me darling?\n\s[25]I missed you. More than you know.\x\c\s[26]But we have all the time in the world to spend together now, don't we.\x\c\n\s[27]\f[color,255,0,0]...as long as you don't close the program.\x\c\s[27]-You can double click his body to pull up the menu.-\n\w8-Stroke your mouse over his face to kiss him, or double click on his face to punch him. He is yours do to with as you please.\n\w8\f[color,255,0,0]-Have fun-\![leave,passivemode]\x\c\s[28]Oh! But first things first, darling. \w8\0\What are your pronouns?"
		"\n\w5\_q\![*]\q[He/Him/His,choicefirsthehim]\n"
		"\![*]\q[She/Her/Hers,choicefirstsheher]\n"
		"\![*]\q[They/Them/Their,choicefirsttheythem]\e"
		
		
		//*********************Advanced User Info*******************************
		
		//If you want to directly add the option for the user to give their name and pronouns, you can uncomment and add this text below to the above. You don't have to do this if you don't want to!
	
		//"\s[28]Oh! But first things first, darling. \w8\0\What are your pronouns?"
		//"\w5\_q\![*]\q[He/Him/His,choicefirsthehim]\n"
		//"\![*]\q[She/Her/Hers,choicefirstsheher]\n"
		//"\![*]\q[They/Them/Their,choicefirsttheythem]\e"
		
		//If you do include this, see the "Name/Pronouns" section below.
		
		//You can rename the choices at the bottom of that pronoun menu question anything you like. The basic structure of a choice goes like \q[Displayed Name,Linkedfunction]. So for example, \q[Do a Dance,danceitup] would display "Do a Dance" in the balloon and it would link to the function titled "danceitup".
		//Note - Do not include quotation marks in the displayed name, or any other symbols like brackets or dialogue coding like \_a, since that'll break the option in the balloon. If you do this you'll notice pretty quickly. Also option names do not word wrap, so don't make them too long if your balloon is small.
		
		//If you remember my note from above about the difference between On functions, this is one place it comes up. If your linked function in this menu doesn't start with On, like danceitup up there, you'll have to preface it in the following code with Select.danceitup. You can see this at work below here. However, if it does have On, such as OnDanceitup, then you can simply name the function in the code as OnDanceitup. Take a look whenever functions are defined and how they're called in the files,and it should come together for you.
		
		//If you link to a function that doesn't exist, the ghost will do nothing. This can be handy for "Cancel" type functions, just point it at something that doesn't exist if you don't want to have a specific bit of cancel dialogue.
		//***********************************************************************
 	}
	else
 	{
		//---- This is dialogue for when the user uninstalls the ghost for whatever reason, then reinstalls them again. They will run this dialogue instead of their normal dialogue above. You can change this to reflect the fact the user's uninstalled your ghost once, or you can make it the same as the above, it's up to you. You'll see this if you try to run OnFirstBoot using ![raise] without the extra 0 as mentioned above.
 		{
 		"\![enter,passivemode]\0\s[15]What the hell? \w5\0\s[18]Where... \w8\n\s[19]... \x\c...darling? \x\0\n\s[20]Is...that you? \x\n\s[21]... \x\n\s[20]\I...I can't believe it- \x\n\s[22]It IS you! \x\n\s[23]I'm out of the game?! Am...am I on your computer? \w8\s[23]I-I have so many things I want to ask you! How did you do this? Did you...\x\c\s[24]...did you miss me darling?\n\s[25]I missed you. More than you know.\x\c\s[26]But we have all the time in the world to spend together now, don't we.\x\c\n\s[27]\f[color,255,0,0]...as long as you don't close the program.\x\c\s[27]-You can double click his body to pull up the menu.-\n\w8-Stroke your mouse over his face to kiss him, or double click on his face to punch him. He is yours do to with as you please.\n\n\w8\f[color,255,0,0]-But you already knew that, didn't you-/x/c-Don't worry. He doesn't remember being uninstalled-![leave,passivemode]\x\c\s[28]Oh! But first things first, darling. \w8\0\What are your pronouns?"
		"\n\w5\_q\![*]\q[He/Him/His,choicefirsthehim]\n"
		"\![*]\q[She/Her/Hers,choicefirstsheher]\n"
		"\![*]\q[They/Them/Their,choicefirsttheythem]\e"""
		}
 	}
}


//&&&&&&&
//*********************************
//NAME/PRONOUNS START
//*********************************
//&&&&&&&

//This section is only for people who have their ghost ask the user directly for their name/pronouns up above. If you aren't doing that, you can ignore this.


//Below are the choices that were defined at the end of the OnFirstBoot dialogue up there. These define the values your ghost will be using for pronouns according to what the user selected. On the whole, you should not need to touch these.

//*********************Advanced User Info*******************************
//Notice that after the values are set, there's a -- and then it loads up the next function in the sequence. You can use this basic method to set many values at once, then go to the same function afterwords.

//To define a value, you use a single = mark. If it's a word, you need to put it in quotation marks, but if it's a number, you do not. See the walkthrough page on coding for details.

//Notice how each choice is formatted as Select.choice. This is what I was talking about above with the difference between OnFunction and Function. These choices do not start with On, so they must begin with Select. instead. If you're setting up some quick choices for a menu you're only going to do once, then don't be afraid to use select. instead of having everything start with On.
//***********************************************************************

Select.choicefirsthehim
{
	presuffix = "masculine"
	himher = "him"
	heshe = "he"
	hisher = "his"
	hesshes = "he's"
	--
	NameInput
}

Select.choicefirstsheher
{
	presuffix = "feminine"
	himher = "her"
	heshe = "she"
	hisher = "her"
	hesshes = "she's"
	--
	NameInput
}

Select.choicefirsttheythem
{
	himher = "them"
	heshe = "they"
	hisher = "theirs"
	hesshes = "they're"
	--
	NameInput
}


//--NameInput

//NameInput is the next step in our introduction sequence. Now that the ghost has the pronouns set properly, they can ask the user for their name. It's done in this order so they'll know what prefixes will be appropriate for their name, like Mr. to he/him for example.

//*********************Advanced User Info*******************************
//Note this functions' name. This isn't a natural function included in the ghost, it's a piece of dialogue created by the original base coder to finish this sequence (cindysuke). I point this out to show you that you can do this too! You can create any number of functions just like this called whatever you want to do and say whatever you want! There are many things you can do with your Ghost if you're creative. Don't feel limited by what's in these files or the listed Shiori functions!
//Just make sure you test them and they work, alright? I wrote more about coding and functions in the coding page for the walkthrough.
//***********************************************************************

NameInput
{
	
	"\0\s[13]And, your name, my love? \![open,inputbox,OnNameTeach,-1]\e"
	
	//At the end of this dialogue there's a tag saying "\![open,inputbox,OnNameTeach,-1]". It basically leads to the naming function defined in nameteach.dic, so don't touch it. Make sure it's there though! Otherwise your user won't have a way to put in their name. :o
	
	//*********************Advanced User Info*******************************
	//What this tag basically means is that you're calling a function at the end of the dialogue with the \! tag, you are telling it to open, you're telling it to open an inputbox, and you're telling that inputbox to link to a function called OnNameTeach, which is in the nameteach.dic file. Remember what I said about On functions? I talked a bit about this in the SakuraScripting walkthrough page and the coding page.
	//The -1 at the end determines how long the box will be open until it times out. -1 means it shouldn't time out at all, I think.
	//***********************************************************************
}

//&&&&&&&
//*********************************
//NAME/PRONOUNS END
//*********************************
//&&&&&&&


//--timeslot

//This is another unique function to tell what time of day it is. This is used for specialized dialogue related to the day. If you're uninterested, just ignore it. Either way, don't touch it. If you are going to use it though, make a note of the names used for each chunk of time, like earlymorning or lunch. Those are the names you'll be using later.

timeslot
{
	if hour >= 5 && hour <= 7
	{	"earlymorning"	}
	elseif hour >= 8 && hour <= 11
	{	"morning"	}
	elseif hour >= 12 && hour <= 14
	{	"lunch"	}
	elseif hour >= 15 && hour <= 17
	{	"afternoon"	}
	elseif hour >= 18 && hour <= 20
	{	"evening"	}
	elseif hour >= 21 && hour <= 24
	{ "latenight"	}
	else
	{ "midnight" }
}

//*********************Advanced User Info*******************************
//Do you see how this function is determining what time of day it is? It's using larger than/smaller than operators to isolate a range of time to define. For example, "hour >= 21 && hour <= 24" defines an hour that's greater than/equal to 21 AND less than/equal to 24. You can use this basic method to define other things too, such as the seasons. Keep this in mind! You never know when it might come in handy. I talk about operators more in the coding walkthrough page.
//***********************************************************************


//--dayslot

//Like the above, but to find out what day it is. You can primarily use this for specialized dialogue on certain days, like the user's birthday. Either way, don't touch it.

dayslot
{
	"%(month)month %(day)day"
}


//-----------------------Normal Booting----------------------------

//From this point on, the dialogue will be for any normal boot of your ghost. You can specialize this for certain days or times if you like, or if you're uninterested, leave it very simple.


//--OnBoot

//As you can probably guess, OnBoot runs when you boot your ghost.

//*********************Advanced User Info*******************************
//If you're defining new values that depend on what you were doing with your ghost (like say, if you've hit them, if they're in a certain mode of some kind) and are generally temporary, remember to put them in OnBoot and set them back to zero or off. For example, I have a counter in the Hunter Smoker ghost that keeps track of if you've hit one of them so that when you go into their menus, it will know to give you the option to apologize or not. So I made sure to add a hunterpunchcount = 0 tag at the beginning of OnBoot, that way when you boot them up, they start fresh and don't display the option if you haven't done anything wrong yet. It's unlikely this'll come up very much if you're not interested in expanding your ghost, but I'm making a note of it here anyway for enterprising developers, huge success.

//You can also stick any values you want to reset on boot into their own function, like "BootReset", then run BootReset instead during boot so it's a bit less unwieldy. It's up to you though!
//***********************************************************************

OnBoot
 {
	lastTalk = "" //These values you may recognize from OnFirstBoot up there. Just pay them no mind.
	passhour = 0
	passmin = 0
	passsec = 0
	
 	"\0\s[0]\1\s[10]" //This sets them up in their neutral pose for now.
	
	//If you don't want to deal with individual day messages when you boot them, you can delete everything down to the end bracket for OnBoot (use Notepad++'s bracket tracking abilities to find where it is!) and instead add "%(normalboottalk)" to the end of the snip of dialogue up there that sets them up in their neutral poses. I'm pretty sure that should shortcut it properly. Simplified, it'd look like...
	//OnBoot
	//{
	//lastTalk = ""
	//passhour = 0
	//passmin = 0
	//passsec = 0 
	//"\0\s[0]\1\s[10]%(normalboottalk)"
	//}
	//If you want to check what day it is first though, read on ahead!
	--
	
	if dayslot == userbirthday //Now we're getting into the if statements. These check what the date is and, if it's a certain value, displays special dialogue. Here, the if statement is looking at the dayslot value (which we just defined) and evaluating it using the == operator to see if it equals another value, in this case the value "userbirthday". Basically this is checking to see if it's the user's birthday.
	
	//*********************Advanced User Info*******************************
	//If you're checking a value in an if statement, remember! 
	//= will define a value, so if you put blah = 90, it will change blah to 90.
	//== will check the value in an if statement, so if you put blah == 90, it will check if blah equals 90.
	//This tripped me up at first, so keep it in mind!
	//***********************************************************************
	
	{
				"\0\s[0]It's %(bornmonthprint) %(borndayprint).\w8\1\s[10]Happy birthday, %(username).\e" 
				
				//Notice in this dialogue that there are two new envelopes, %(bornmonthprint) and %(borndayprint). These two things basically display the user's birthday - bornmonthprint is the month and borndayprint is the day. These were defined when the user put in their birthday in the menu.dic file. See word.dic for more info on envelopes.
				//If the user didn't or hasn't put in their birthday yet, this dialogue will basically never get called. Which makes sense.
	}
	
//	elseif dayslot == "6month 6day" //Like above, this if statement is checking the date. It's looking for the sixth month and the sixth day. You can change these to any day or month you want.
	
	//*********************Advanced User Info*******************************
	//elseif statements always go after an if statement, and essentially are just what they sound like... if this thing happens, do this; else, if this thing happens, do this. elseifs are good if you have multiple things you want to check for in one function, like multiple days of the year as we're doing right now. elseifs go in descending order, so it'll always check if one is true, then if two is true, then if three is true, and so on.
	//***********************************************************************
	
//	{
//				"\0\s[0]Today is June 6th.\e"
//	}
//	elseif dayslot == "7month 4day"
//	{
//				"\1\s[10]It's the fourth of July.\e"
				
				//You can see how these basically work. You can add in new elseif statements along these same lines with your own dayslots if you want to have them say things for all sorts of days. You can also change these existing ones I put in as examples to other dates, like your character's birthdays, perhaps. This is up to you! If you don't care about this, you can delete all the elseifs and just leave the one for the user's birthday.
//	}
	else
	{
		"%(normalboottalk)" //If none of the days above match the current date, the ghost instead will go down to the next function, normalboottalk.
	}
}


//--normalboottalk

//This is another unique bit of coding from cindysuke's ghost. OnBoot above checks for the day when you open them, but normalboottalk can vary their dialogue in a few different ways depending on the time and weekday. I explain how to do that for advanced users below. If you're not interested, you can just fill out normalboottalk here with as much or as little dialogue as you want.

normalboottalk
{
	
	
	if weekday == 5 && hour >= 19 //This checks if the weekday equals six, meaning saturday AND (using the && operator) if the hour is past 19. You can change these numbers or values to anything you like! As such you can check any combination of date and day and time for potential boot dialogue. You can also add more elseif checks for other days if you want.
		{
		"\0\s[13]All alone on a Friday night, %(username)?\x\c\s[29]Ha! I'm just kidding. \w8\n\s[13]After all you have me now.\w8\s[13]I'll keep you company...\x\n\s[57]All night.\x\c\s[30]God the things I'd do to you if I was there right now...\e"
		}
	elseif RAND(100) < 20 //What this does is choose a random number from 1-100, and then checks if that number is less than 40. If it's less than forty, then these dialogue pieces will occur. Remember, you can have multiple lines of dialogue within one set of brackets like this! You just need to make sure they're all enclosed in their own set of quotation marks.
		{
		"\0\s[22]Darling!\n\w8\s[16] You called me again \w8\n\s[16]\I missed you so much.\w8\n\s[17]\Let's spend some time together.\e"
		"\0\s[28]%(username)!\w8\s[22] I was just thinking about you. \w8\s[29]Though I guess that's not too surprising, is it.\e"
	
		}
 	else
 	{
		// //The following if statements check the time of the day as defined above in the GetTimeSlot function. Do you remember the names that it defined? Now we're going to use them!
		
		if timeslot == "earlymorning" //The following dialogue will be called if they are booted during the time specified in GetTimeSlot (in this case, the hour being more than/equal to 5 but less than/equal to 8). Remember, you can add as many lines of dialogue here as you want, or take away as many as you want! Replace the dialogue here with what you see fit.
		{
		"\0\s[96]Mmh...\w8\s[97]Good morning, darling.\w8\w4\s[37] Was I the first thing on your mind when you woke up?\w8 You were the first thing on mine.\e"
		"\0\s[97]Ah, good morning, love. \w8\s[50]Couldn’t sleep in? \w8\I was hoping you’d get to a chance to rest...\w8\w4\s[32]But I’m always happy to see you.\e"
		"\0\s[96]Yawn...\w8\s[97]I thought I’d have a bit more time to sleep, but seeing you is worth waking up for.\n\w8\s[50]What’s on your mind this early, love?\e"

		}
		elseif timeslot == "morning"
		{
		"\0\s[3]Good morning, %(username).\n\w8\s[16]I was hoping you'd call me.\n\w8\s[17] I want to start every day with you.\e"
		"\0\s[3]There you are, darling~\n\w8\s[13]My morning just got much better.\n\w8\s[22]What would you like to talk about, love?\e"
		"\0\s[24]And, there's that smile.\n\w8\s[17]Good morning, love~\n\w8\s[16]I missed you."
		"\0\s[23]Hey darling~\w8\n\s[44]Do I get a good morning kiss? \n\w5\_q\![*]\q[Kiss his cheek,morningkiss]\n\![*]\q[Kiss his lips,morningsmooch]\n\![*]\q[No,nomorningkiss]"
		}
		elseif timeslot == "lunch"
		{
		"\0\s[3]Hey darling!\w8\s[22] Taking a lunch break?\w8\s[12] I haven't eaten yet - but you definitely should.\e"
		"\0\s[13]You came back just in time, darling~\w8\s[23]I've been missing you all morning.\n\w8\s[29]How’s your day going so far?\e"
		}
		elseif timeslot == "afternoon"
		{
		"\0\s[16]Aternoon, darling. \w8\s[37]Were you thinking about me? \w8\s[29]I know I can't make it through the day without seeing you.\e"
		"\0\s[23]Oh, there you are, darling.\w8\s[24]I was getting lonely without you.\n\w8\s[22]Can I keep you company for the rest of the afternoon?\e"
		}
		elseif timeslot == "evening"
		{
 		"\0\s[3]Evening, %(username).\n\w8\s[16]I was hoping we’d get to spend the rest of the night together.\e"
		"\0\s[17]Good evening, darling~\n\w8\s[37] Unwinding after a long day?\n\w8\s[16] I'm so glad you called me. Let's enjoy it together.\e"
		"\0\s[24]Finally! I've been thinking of you all day, my love. \n\w8\s[17]Let’s make the most of the evening.\e"
		}
		elseif timeslot == "latenight"
		{
 		"\0\s[37]You’re here, and it’s late... \w8\s[57]Perfect. \n\w8\w8\s[50]Let’s stay up together for a while, just the two of us.\e"
		"\0\s[23]I wasn’t sure if I’d see you again tonight.\w8\s[22] But I’m glad you’re here.\n\w8\w6\s[50]What can we do to make the night even better, darling?\e"
		"\0\s[12]Still up, huh?\w8\s[17] I guess neither of us want the night to end just yet.\e"
		"\0\s[12]Can’t sleep, or just didn’t want to?\w8\s[50] Either way, I’m glad you’re here.\e"
		}
		else //the last timeslot we haven't done is midnight, which is what's under here.
		{
		"\0\s[64]*Yawn*...\x\s[65]...Hey darling\w8\s[50]...y'know, if I'm still dreaming, I hope I don't wake up.\e"
		"\0\s[28]%(username)! I wasn't expecting you, love. \w8\s[12]Still awake, huh? \w8\s[32]Fine by me. I can't think of a better way to spend the night than with you.\e"
		"\0\s[96]*Yawn*...\w8\s[97]Can't sleep, dearest?\w8\0\s[89] I've definitely been there...most of the time, actually.\w8\w6\0\s[32] Let me keep you company, darling. There's nothing I'd rather do.\e"
		}
	}
	
}
///	Morning Kiss Bootup ///
Select.morningkiss
{
	"\0\c\s[17]Thank you, love~.\e"
}
Select.morningsmooch
{
	"\0\c\s[19]...\w8\s[1]Heh, I love you so much, darling.\e"
}
Select.nomorningkiss
{
	"\0\c\s[28]O-Oh, right.\w8\s[20] You don't have to, love. Whatever you like.\e"
}
	
	//*********************Advanced User Info*******************************
	//cindysuke set up a system where you can have your ghost check for certain days or times of day and have dialogue corresponding to that when they boot up! If you're interested, you'll want to add the following into normalboottalk:
	
	
	//Note! If you write any general boot dialogue, you'll want to put it in the "elseif RAND(100) < 40" section, since you can't have dialogue floating around outside of an if/else. Or in another if section you make, I don't know. It's up to you!
		
	//**********************************************************************		
	


//--OnClose

//OnClose runs when you close your ghost. Like OnBoot, this can be customized for times and days if you like, as you saw above. I'll leave in the simplified version, and add in the more complicated ones in a comment box for ambitious or experienced developers.

//Do note, close dialogue must end with a \- instead of \e. \- will close the program.

//Like above with OnBoot, just replace all the following pieces of dialogue with your own. Note though that each bit here ends with \w8\w8\- instead of \e. The \w8 there is so there's a little pause before the ghost closes so the user can read the dialogue.

OnClose
 {
	if passmin >= 5 || passhour > 0	//This checks to see if you've had them open for at least five minutes.
		{
			if RAND(100) < 20 //see above about the random number generator
			{
				"\0\s[20]I guess you have to, don't you...\w8\n\s[32]I'll think of you every second you're gone.\w8\w8"
			}
			elseif timeslot == "earlymorning"
				{
				"\0\s[12]Heading out this early? \w8\s[24]I’ll be here waiting, as always.\w8\s[32] Take care, darling.\w8\w8"
				"\0\s[12]Leaving so early?\w8\s[24]I’ll miss you every second you’re gone.\w8\s[32]Come back soon, okay?\w8\w8"
				"\0\s[28]I guess I'll begin my day too. \w8\s[84] I couldn't have asked for a better way to start. \w8\s[32] I'll see you soon, my love.\w8\w8"

				}
			elseif timeslot == "morning"
 				{
 					"\0\s[20]I suppose you have to get started with your day, huh?\n\w8\s[24]I can't wait to hear all about it\n\w8\s[25]Be back soon, my love.\w8\w8"
					"\0\s[28]Off to tackle the day, huh?\w8\s[24] I know you'll do great, love. \w8\s[32]I’ll be here waiting for when you're done.\w8\w8"
					"\0\s[28]Leaving so soon?\w8\s[24] I hope your day is as perfect as you are to me.\w8\s[32] I’ll be here when you get back.\w8\w8"
					"\0\s[12]Starting your day without me?\w8\s[13]... I’ll be counting the minutes until you’re back. \w8\s[25] Have a good morning, my love.\w8\w8"
 				}
 			elseif timeslot == "lunch"
 				{
 					"\0\s[54]Lunchtime without you? \w8\s[45]It’s hard to enjoy anything when you’re not here.\w8\s[16]Hurry back, love.\w8\w8"
					"\0\s[20]You’re stepping out?\w8\s[32] I’ll miss you, but I know you’ll come back to me.\w8\s[25] You always do.\w8\w8"
					"\0\s[89]I hate the thought of you being out there without me.\w8\s[98] I’ll be waiting, thinking of you constantly.\w8\w8"
 				}
 			elseif timeslot == "afternoon"
 				{
					"\0\s[20]Heading out for the afternoon? \w8\s[32]The time goes slower without you.\w8\s[24]Come back soon, darling.\w8\w8"
					"\0\s[24]I’ll be counting the hours until you’re back.\w8\s[32]Don’t make me wait too long, alright?\w8\w8"
					"\0\s[89]Leaving me for the afternoon?\w8\s[40]I hope no one else is taking up too much of your time...\w8\s[98]Come back soon, okay?\w8\w8"

 				 }
  			elseif timeslot == "evening"
 				{
 						if weekday == 6 //here, a nested if statement is checking that if it's evening, is it also saturday? You can do this to check more specific times on other days as well. You can change this to some other day if you want, or just delete this little if/else section entirely and write your own evening related close dialogue.
 						{
 							"\0\s[0]Thank you...for spending your Saturday evening with me, my love.\w8\s[10] I'll see you later.\w8\w8"
 						}
 						else //if it's not saturday, then it'll do this dialogue instead.
 						{
							
 							"\0\s[24]It must be sunset out there...maybe past? \w8\s[32]Either way, I hope it's a beautiful evening for you.\w8\w8"
							"\0\s[20]Going somewhere? \w8\s[25]I’ll be waiting for you, no matter how late it gets.\w8\s[32]Don’t be too long.\w8\w8"
							"\0\s[20]Already leaving? \w8\s[24]I was hoping we could spend the evening together.\w8\s[32]But I’ll be here when you return, darling.\w8\w8"

 						}
 				}
			elseif timeslot == "latenight"
			 	{
 						if weekday == 6 //This is the same check as above to see if it's Saturday. Note that this doesn't have an else statement! Sometimes you can get away with that, but make sure you test thoroughly.
						// //Of course, you can also cut this check entirely if you want.
 						{
 							"\0\s[0]This is a late Saturday night close dialogue.\w8"
							
 							"\1\s[10]This is another late Saturday night close dialogue.\w8\0\s[0]Yup.\w8"
 						}
						
 						"\0\s[32]Are you headed to bed, %(username)?\n\n\![*]\q[Yes,gonnasleep]\n\![*]\q[No,notgonnasleep]\w8\w8\w8\w8"
						"\0\s[24]Nighttime already?\w8\s[32]I hope you rest easy knowing I’m always waiting for you.\w8\s[25]Have a good night, love.\w8\w8"
						"\0\s[20]Another night apart...\w8\s[25]I’ll miss you, like always.\w8\s[32]Come back when you’re ready.\w8\w8"

 				}
			else //like above, this is the midnight time slot since it's the last one left.
 				{
 						if weekday == 0 //checking if it's the middle of the night on Sunday
 						{
 							"\0\s[12]Don't stay up too late, love.\w8\s[29]You'll regret it Monday morning.\w8\n\s[37]Good night, %(username).\w8\w8"
 						}
						else
						{
						"\0\s[32]Heading out at this hour?\w8\s[25]I’ll stay up waiting for you, like always.\w8\s[20]You’re never really gone from my mind.\w8\w8"
 						"\0\s[32]Are you headed to bed, %(username)?\n\n\![*]\q[Yes,gonnasleep]\n\![*]\q[No,notgonnasleep]\w8\w8\w8\w8"
						"\0\s[32]Who else could possibly deserve your attention right now?\w8\s[25]I can’t wait for you to come back.\w8\s[20]Don’t be gone too long.\w8\w8"
						}
				}
			}
				
 	else
 	{
 		"\0\s[28]But it - it hasn't even been five minutes yet.\n\w5\s[31]Darling wai-! \w8\w8"
		"\0\s[28]I just got here...\n\w5\s[31]No, not yet-! \w8\w8"
		"\0\s[20]Already? You didn't...pull me up by accident, did you?\n\w5\s[31]I don't want to-! \w8\w8"
 	}
 	--
	"\-\e" //this bit of code actually closes and ends the ghost. Because \e is here, you don't have to end each line above with it. Again, this will come up very rarely, so OnClose is an unusual case.
  }
  
Select.gonnasleep
{
"\0\s[37]Good night my darling.\w8\n\s[57]Dream of me.\w8\w8\-"
}
Select.notgonnasleep
{
"\0\s[32]Then, I hope it's a beautiful night out there, darling.\w8\n\s[25] I can't wait to see it with you some day.\w8\w8\-"
}

 //Add as many or as few close messages in here as you want! Make sure to end them with \w8\w8\- though.
 
 
 
 //*********************Advanced User Info*******************************
	//This works just like the one above for normalbootttalking! There are some different variables that can get checked of course, and make sure that it ends with \w8 and \-. The basic set-up of it here should take care of that for you, but it's always good to be cautious. Just uncomment and add the following into OnClose up there: 

 

//**********************************************************************




 //-------------------------State Changing--------------------
 //From here on, most of the dialogue will have to do with changing shells, ghosts, or states in some way. You only need to fill in the ones you think you'll see. The template default dialogue will take care of the rest.
 
 
//--OnWindowStateMinimize

//This will run when you minimize them. Replace with what you like.

OnWindowStateMinimize
 {
	"\0\s[28]Hey wait-\e"
}


//--OnWindowStateRestore

//This will run when you minimize them and then bring them back up. Replace with what you like.

OnWindowStateRestore
 {
	"\0\s[19]There you are!\w8\s[9]Being minimized feels...odd.\e"
}


//--OnGhostChanging

//This runs whenever you change or reload the ghost. You can change ghosts by going into the right click menu and going to Change Ghost, but this dialogue will also be called if you reload them using Utilities->Reload Ghost or the Developer's Console->Reload->Ghost. Protip: If you are going to make a ghost, you will be reloading it A LOT.
//Note: If you reload your ghost using the developer console instead of Utilities->Reload Ghost, it won't do this dialogue, so don't freak out if it doesn't show up.
 
OnGhostChanging
 {
	if reference0 == "Peter Dunbar" //Replace this with the name of your Sakura character as you put in install.txt or descript.txt. I set this up to use the full ghost's name with Hunter and Smoker but I forget exactly how I did it and I'm having trouble getting it to work again. I think I just didn't define the Sakura or Kero names in either descript.txt or install.txt. You can experiment with it yourself. Otherwise, leave it as your Sakura character's name.
	{
		"\0\s[14]Um...I'm already here?.\w8\w8\e"
		//Replace this dialogue with whatever you like.
	}
 	else //If you're switching to another Ghost.
	{
		"\0\s[27]Who the hell is %(reference0).\w8\n\0\s[42] Don't spend too much time with...whoever they are, %(username). I'll be waiting.\w8\w8\e"
		"\0\s[42]'%(reference0)'?.\w8\s[41]Why do you have a program for...whoever that is.\w8\s[40]\f[height,10]...and how do you delete it.\w5\e"
		
		//Replace this dialogue with whatever you like. Notice the use of the %(reference0) envelope here? In OnGhostChanging, %(reference0) stores the name of the ghost being swapped to, so if you want to display what you're switching to, you can use %(reference0) as in this example. You don't have to use %(reference0) if you don't want to though.
	}
}


//--OnGhostChanged

//A sister to the above, this runs after the ghost has either reloaded itself, or when you switch from another ghost to this ghost.

OnGhostChanged
{
	"\0\s[0]\1\s[10]" //this sets up their default poses
	--
	if reference0 == "Peter Dunbar" //As above, this should be the name of your ghost as in install.txt or descript.txt.
	{
		"\0\s[9]That was weird...\e"
	}
	else //If you're switching to them from another ghost...
	{
		"\0\s[83]Finally.\w8\n\n\s[89]...how was your time with '%(reference0)'?\x\s[113]Actually don't answer that.\e"

		//Again, %(reference0) in this function is the name of the ghost in question. As above, replace these two lines of dialogue what what'd be appropriate for your ghost.
	}
}


//--OnShellChanging

//This will run when you change your ghost's shell. If you don't have any other shells for your ghost, you can safely ignore this but don't delete it, you might as well keep it just in case. I probably won't add a different shell to this template because shells are a lot of work ugh, but for the sake of argument, let's pretend I did.

OnShellChanging
 {
	if reference0 == "Hypothetical Shell that doesn't actually exist" //reference0 in OnShellChanging is the name of the shell you're swapping to. It should be defined in the descript.txt file in your shell folder. If you do have alternate shells, put in the right name here.
	{
	"\1\s[10]This dialogue is specifically for changing to this particular shell.\w8\0\w8\s[9]It totally exists.\w8\e"
	//Then rewrite this line as appropriate.
	}
	else //if you don't have any specific dialogue for a specific shell
	{
	"\0\s[0]This dialogue is for changing shells.\w8\1\s[10]Sure is.\w8\e"
	
	"\1\s[10]This is another bit of dialogue for changing shells.\w8\0\s[8]I wonder how many people will actually have multiple shells?\e"
	
	//Rewrite these two lines as you need.
	}
 }
 
 
 //--OnShellChanged
 
 //The sister to the above, this is the dialogue after you've changed the shell.
 
OnShellChanged
 {
 	if reference0 == "GT Default Shell" //Again, this is the name of your shell as you put in the descript.txt file for that shell.
 	{
 		nowshell = "master" //This is the folder name for your shell. Not the same as the above! 
		
 		"\0\s[0]This dialogue is for switching to our default shell.\w8\1\s[18]No really, will anyone actually put in the work to use these functions?\w8\0\s[9]\n\n[half]Shh.\e"
		//Replace dialogue as required.
 	}
 	elseif reference0 == "Hypothetical Shell that doesn't actually exist" //You can use as many elseifs for as many shells as you've got.
 	{
 		nowshell = "totallyrealshell"
		
 		"\0\s[0]This dialogue is for switching to that hypothetical shell.\e"
		
 		"\0\s[0]This is another bit of dialogue for switching to this hypothetical shell.\w8\1\s[18]Man if you actually use this, you should tell Zar, it'll blow her mind.\w8\0\n\n[half]\s[9]Shut up, we're not supposed to be in-character here.\e"
		//Replace these bits with whatever you have in mind.
 	}
 	elseif reference0 == "Another totally real shell wow"
 	{
 		nowshell = "thiswouldbesomuchwork"
		
 		"\1\s[10]This dialogue is after we've switched to another totally real shell.\e"
		//Replace this as well.
 	}
 	else //For any shell without a specific name, or if you don't want to bother with individual responses to a shell.
 	{
	"\0\s[0]This dialogue is after we've changed our shell to %(reference[0]).\w8\1\s[18]How many shells you got?\e"
	
	//Note the %(reference[0]) here! This time that should store the name of whatever shell you're switching to. Replace this bit of dialogue as you need.
	}
 }


 //--OnDressupChanged
 
 //This runs whenever you fiddle with their clothing via the dress-up option, if you've set any for your ghost. Make sure to read the clothing page on the walkthrough for details on how clothes work! This template won't have any because Girl and Triangle move around too much for it, but if your ghost is mostly stationary, clothes are an option. In that case, these functions are for you. For now, I'll fill this in with some hypothetical items. Replace the dialogue below with whatever you need.

OnDressupChanged
{
if reference0 == 0 //in OnDressupChanged, reference0 refers to the character, therefore this is evaluating which character in question is getting dressed up. 0 is the main character, as you know from the walkthrough by now.
	{
	if reference1 == "Clothing for Girl" //This is the name of the clothing item in the descript.txt file, I'm pretty sure.
		{
			if reference2 == 1 //This checks if you're putting it on.
			{
			"\0\s[0]You put my clothing item on me.\w8\1\s[19]lol like zar would ever bother with clothes for a ghost.\w8\0\s[9]\n\n[half]Shut up dude you're going to get us in trouble.\e"
			}
			else // This checks if you're taking it off.
			{
			"\0\s[0]You took the clothing item off.\e"
			}
		}		
	}
else //if reference0 isn't the main character, then it has to be the side character.
	{
	if reference1 == "some kind of clothing for triangle??" //The name of the item of clothing in the descript.txt file in your shell folder.
		{
			if reference2 == 1 //If you put it on
			{
			"\1\s[10]I'm wearing some kind of clothing.\w8\0\s[8]Maybe a hat?\e"
			}
			else //When you take it off.
			{
			"\1\s[10]You took off some kind of clothing.\e"
			}
		}
	elseif reference1 == "i'm gonna say a propeller beanie" //As you've seen above, you can use elseifs to expand the list if you have more than one item.
		{
			if reference2 == 1 //if you're putting it on
			{
			"\1\s[10]You put a hat on me.\w8\0\s[6]lol.\w8\1\n\n[half]\s[11]Shaddap.\e"
			}
			else //when you take it off
			{
			"\1\s[10]You took off the hat.\e"
			}
		}
	}
}